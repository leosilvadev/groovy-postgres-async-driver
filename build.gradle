plugins {
	id 'groovy'
	id 'eclipse'
	id 'jacoco'
	id 'com.github.kt3k.coveralls' version '2.6.3'
	id 'maven'
	id 'signing'
}

sourceCompatibility = 1.8

repositories { mavenCentral() }

dependencies {
	compile 'org.codehaus.groovy:groovy-all:2.4.+'
	compile group: 'com.github.alaisi.pgasync', name: 'postgres-async-driver', version: '0.9'

	testCompile group: 'org.spockframework', name: 'spock-core', version: '1.0-groovy-2.4'
}

test {
	filter { includeTestsMatching "*UnitSpec" }
}

task integrationTest(type: Test) {
	filter { includeTestsMatching "*IntegrationSpec" }
	dependsOn test
}

jar {
	manifest {
		attributes 'Implementation-Title': 'groovy-postgres-async-driver',
		'Implementation-Version': version
	}
}

tasks.check { dependsOn integrationTest }

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts { archives javadocJar, sourcesJar }

jacocoTestReport {
	executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
	reports {
		xml.enabled = true
		html.enabled = true
	}
}

if ( !System.env.CI ) {
	signing { sign configurations.archives }
	
	uploadArchives {
		repositories {
			mavenDeployer {
				beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
	
				repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
					authentication(userName: ossrhUsername, password: ossrhPassword)
				}
	
				snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
					authentication(userName: ossrhUsername, password: ossrhPassword)
				}
	
				pom.project {
					name 'Groovy Postgres Async Driver'
					packaging 'jar'
					description 'Groovy wrapper for postgres-async-driver, adding utils methods and clue.'
					url 'https://github.com/leosilvadev/groovy-rx-postgres-async-driver'
	
					licenses {
						license {
							name 'The Apache License, Version 2.0'
							url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						}
					}
	
					developers {
						developer {
							id 'leosilvadev'
							name 'Leonardo Silva'
							email 'leosilvadev@gmail.com'
						}
					}
				}
			}
		}
	}
}
